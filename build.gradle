plugins {
	id 'java'
	id 'eclipse'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'etri'
version = '0.0.1'

sourceCompatibility = '17'
targetCompatibility = '17'
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
	mavenCentral()
}

ext {
	logback_version = '1.3.14'
	lombok_version = '1.18.32'
	
	aas4j_version = '1.0.0'
	postgresql_version = '42.3.3'
	spring_boot_version = '3.2.4'
	h2_version = '2.2.224'
	paho_version = '1.2.5'
	
	docker_client_version = '7.0.8'
	jersey_version = '3.1.6'
	fabric8_version = '6.9.2'
}

dependencies {
	implementation project(":utils")
	implementation project(":mdt.client")
	
	// Logback with SLF4j
	implementation "ch.qos.logback:logback-classic:${logback_version}"

	// Lombok
	compileOnly "org.projectlombok:lombok:${lombok_version}"
	annotationProcessor 'org.projectlombok:lombok'

	// AAS model
	implementation "org.eclipse.digitaltwin.aas4j:aas4j-dataformat-json:${aas4j_version}"

	// Spring boot
	implementation "org.springframework.boot:spring-boot-starter-web:${spring_boot_version}"
	testImplementation "org.springframework.boot:spring-boot-starter-test:${spring_boot_version}"
	
	// Postgresql
//	implementation "org.postgresql:postgresql:${postgresql_version}"
	// H2
	implementation "com.h2database:h2:${h2_version}"
	
	// MQTT
	implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:${paho_version}"
	
	
	// Docker Java Binding
	implementation "org.mandas:docker-client:${docker_client_version}"
	implementation "org.glassfish.jersey.core:jersey-client:${jersey_version}"
	implementation ( "org.glassfish.jersey.connectors:jersey-apache-connector:${jersey_version}" ) {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
	implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jersey_version}"
	implementation "org.glassfish.jersey.inject:jersey-hk2:${jersey_version}"
	
	// Kubernetes client
	implementation "io.fabric8:kubernetes-client:${fabric8_version}"
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

bootJar {
	archiveFileName = "${baseName}-${version}-all.jar"
	mainClass = 'mdt.MDTApplication'
}

tasks.named('test') {
	useJUnitPlatform()
}

eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'build/classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						if ( entry.path.contains('/test/') ) {
							entry.output = 'build/test_classes'
						}
						else {
							entry.output = 'build/classes'
						}
					}
				}
			}
		}
	}
}